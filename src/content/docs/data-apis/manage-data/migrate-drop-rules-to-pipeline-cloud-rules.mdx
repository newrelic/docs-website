---
title: Migrate from drop rules to Pipeline cloud rules
tags:
  - Data Management
  - Migration
  - Drop Rules
  - Pipeline Control
  - Terraform
  - GitOps
metaDescription: Migrate your NRQL drop rules to Pipeline cloud rules using Terraform with GitOps (CI/CD) or on your local machine.
freshnessValidatedDate: never
---

<Callout variant="important">
As of May 21, 2025, new customers can no longer use NRQL drop rules. [NRQL drop rules will be end-of-life](/eol/2025/05/drop-rule-filter) on January 7, 2026.

Migrate to [Pipeline cloud rules](/docs/new-relic-control/pipeline-control/cloud-rules-api) to continue managing your data dropping rules.
</Callout>

Although New Relic automatically migrated all drop rules to Pipeline cloud rules in the New Relic application, Terraform users still have a manual migration step. As Terraform maintains its own state file, it only knows about the `newrelic_nrql_drop_rule` resources you explicitly configured. It doesn't automatically understand that these are now linked to `newrelic_pipeline_cloud_rule` resources in the backend.

Since Terraform treats these as two completely different resource types, you'll need to manually migrate by importing the Pipeline cloud rules into Terraform's state and removing the old drop rule resources. This guide helps you migrate Terraform-managed drop rules to Pipeline cloud rules.

## Migration approaches [#migration-approaches]

This guide covers two ways to migrate your NRQL drop rules to Pipeline cloud rules in Terraform:

1. Native Terraform commands: The standard Terraform workflow using `terraform import`, `terraform plan`, and `terraform state rm` commands.
2. New Relic CLI automation tools: New Relic provides CLI tools that automate the native Terraform workflow, available in two versions:
   - GitOps (CI/CD): For teams using CI/CD tools.
   - Local Terraform: For teams executing Terraform commands on their local machines.

We recommend using New Relic's automation tools as they significantly reduce manual work and potential for errors. However, if you prefer complete control or have specific requirements, you can follow the native Terraform command approach.

## Migrate with native Terraform commands [#native-terraform]

This section describes the standard Terraform workflow for migrating from `newrelic_nrql_drop_rule` resources to `newrelic_pipeline_cloud_rule` resources using native Terraform commands.

### Prerequisites [#native-prerequisites]

- Terraform or OpenTofu >= v1.5: This version introduced the `import` block feature, which makes the migration process more efficient by eliminating the need to manually write resource configurations.
- New Relic Terraform Provider >= v3.68.0: This version adds support for the `pipeline_cloud_rule_entity_id` attribute on `newrelic_nrql_drop_rule` resources and introduces the `newrelic_pipeline_cloud_rule` resource.

### Migration steps [#native-steps]

<Steps>
  <Step>
    #### Get the Pipeline cloud rule IDs [#get-pipeline-cloud-rule-ids]

    Upgrade the New Relic Terraform Provider to v3.68.0 or greater, then refresh and apply your configuration with the existing `newrelic_nrql_drop_rule` resources. This allows the `pipeline_cloud_rule_entity_id` attribute to be exported by each drop rule resource.

    ```bash
    terraform refresh
    terraform apply
    ```

    After running the above commands, you'll get the Pipeline cloud rule entity ID for each drop rule. Keep the IDs handy for the next step.

    ```hcl
    # Example: Get the entity ID for a drop rule
    newrelic_nrql_drop_rule.foo.pipeline_cloud_rule_entity_id
    ```
  </Step>

  <Step>
    #### Import the Pipeline cloud rules into Terraform state [#import-pipeline-cloud-rules]

    Use the `terraform import` command with the Pipeline cloud rule entity IDs obtained in the previous step to import each rule as a `newrelic_pipeline_cloud_rule` resource.

    ```bash
    terraform import newrelic_pipeline_cloud_rule.foo <pipeline_cloud_rule_entity_id>
    ```

    <Callout title="Note">
      The ID of a Pipeline cloud rule is not the same as the ID of the corresponding NRQL drop rule. You must use the `pipeline_cloud_rule_entity_id` attribute exported by the drop rule resource to get the correct Pipeline cloud rule ID.

      You can pair the import with `terraform plan -generate-config-out` to automatically generate the corresponding resource configuration for the imported Pipeline cloud rules.

      {/* ```hcl
      import {
        to = newrelic_pipeline_cloud_rule.foo
        id = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZjgtYmFmNy03MjU3LWE3M2MtZWY5OTkxYTQxMjgy"
        # id = newrelic_nrql_drop_rule.foo.pipeline_cloud_rule_entity_id
      }
      ```

      Then run:

      ```bash
      terraform plan -generate-config-out=pipeline_cloud_rules.tf
      terraform apply
      ``` */}
    </Callout>
  </Step>

  <Step>
    #### Remove the old drop rule resources from Terraform state [#remove-old-drop-rules]

    After successfully importing the Pipeline cloud rules, you should remove all references to `newrelic_nrql_drop_rule` resources from your Terraform state using the `terraform state rm` command.

    ```bash
    terraform state rm newrelic_nrql_drop_rule.foo
    ```

    This removes the drop rules from Terraform state without deleting them from New Relic.
  </Step>

  <Step>
    #### Clean up your Terraform configuration [#clean-up-terraform-configuration]

    Run the `terraform plan` command to remove the old `newrelic_nrql_drop_rule` resource definitions from your Terraform configuration files and verify the migration.

    ```bash
    terraform plan
    ```

    The output should show "No changes" if the migration was successful.
  </Step>
</Steps>

## Migrate with New Relic's automation tools [#automation-tools]

To simplify the native Terraform migration workflow described above, New Relic provides CLI automation tools that handle the import and state management for you. These tools automate the same underlying Terraform operations (import, plan, apply, and state removal) but with significantly less manual work.

You can choose between two approaches based on your Terraform workflow:

- **GitOps (CI/CD):** For teams using CI/CD tools to manage Terraform.
- **Local Terraform:** For teams executing Terraform commands directly on their local machines.

Both approaches use the New Relic CLI to streamline the migration process and reduce the risk of errors.

### Prerequisites [#automation-prerequisites]

- Terraform or OpenTofu >= v1.7: Required for the automated migration approaches.
- New Relic Terraform Provider >= v3.68.0: This version adds support for the `pipeline_cloud_rule_entity_id` attribute and the `newrelic_pipeline_cloud_rule` resource.
- New Relic CLI: Required to run the automated migration commands.

### Choose your approach [#choose-approach]

<CollapserGroup>
  <Collapser
    id="gitops-cicd"
    title="Using GitOps (CI/CD)"
  >
    <CollapserGroup>
      <Collapser
        id="gitops-step1"
        title="1. Map existing drop rules to Pipeline cloud rules"
      >
        In this step, you'll add a validation script provided by New Relic to your CI/CD Terraform configuration that maps your existing drop rules to their corresponding Pipeline cloud rules.

        <Steps>
          <Step>
            #### Find your drop rule resources [#find-drop-rule-resources]

            Look through your Terraform configuration files (`.tf` files) in your workspace and identify all your `newrelic_nrql_drop_rule` resources.

            For example, if your Terraform configuration file has these drop rules:

            ```hcl
            resource "newrelic_nrql_drop_rule" "drop_debug_logs" {
              account_id  = var.new_relic_account_id
              description = "Filters out debug level logs"
              action      = "drop_data"
              nrql        = "SELECT * FROM Log WHERE level = 'debug'"
            }

            resource "newrelic_nrql_drop_rule" "drop_health_checks" {
              account_id  = var.new_relic_account_id
              description = "Removes health check logs"
              action      = "drop_data"
              nrql        = "SELECT * FROM Log WHERE endpoint = '/health'"
            }

            resource "newrelic_nrql_drop_rule" "drop_pii_data" {
              account_id  = var.new_relic_account_id
              description = "Filters out PII data"
              action      = "drop_data"
              nrql        = "SELECT * FROM Log WHERE contains(message, 'SSN')"
            }
            ```

            In resource `"newrelic_nrql_drop_rule" "drop_debug_logs"`:

              - The resource identifier is `drop_debug_logs`.
              - The full resource reference is `newrelic_nrql_drop_rule.drop_debug_logs`.

            Make a note of each resource identifier and their corresponding full resource references. You'll need these in the next step.
          </Step>

          <Step>
            #### Add the validation script to your Terraform configuration [#add-validation-script]

            Create a new file (for example, `outputs.tf`) in your Terraform workspace and add the following script:

       ```hcl
            # Local list of all drop rule resources in the experimental folder
            locals {
            experimental_drop_rule_resources = [
                {
                name     = "drop_debug_logs"
                resource = newrelic_nrql_drop_rule.drop_debug_logs
                },
                {
                name     = "drop_health_checks"
                resource = newrelic_nrql_drop_rule.drop_health_checks
                },
                {
                name     = "drop_pii_data"
                resource = newrelic_nrql_drop_rule.drop_pii_data
                }
            ]

            # Validation logic compatible with Terraform 1.2.x
            validation_results = [
                for drop_rule in local.experimental_drop_rule_resources : {
                name      = drop_rule.name
                entity_id = try(drop_rule.resource.pipeline_cloud_rule_entity_id, null)
                is_valid  = try(drop_rule.resource.pipeline_cloud_rule_entity_id, null) != null
                }
            ]

            # Create error message if any validation fails
            validation_errors = [
                for result in local.validation_results :
                "❌ Drop rule '${result.name}' does not export `pipeline_cloud_rule_entity_id` or it is null"
                if !result.is_valid
            ]

            # Pre-computed JSON object to avoid complex expressions in output
            json_data_object = {
                drop_rule_resource_ids = [
                for drop_rule in local.experimental_drop_rule_resources : {
                    name                        = drop_rule.name
                    id                          = drop_rule.resource.id
                    pipeline_cloud_rule_entity_id = length(local.validation_errors) == 0 ? drop_rule.resource.pipeline_cloud_rule_entity_id : null
                }
                ]
            }

            # Pre-computed formatted JSON string to avoid heredoc issues in conditional expressions
            formatted_json_string = <<-EOT
            {
            "drop_rule_resource_ids": [
            ${join(",\n", [for drop_rule in local.experimental_drop_rule_resources : "    {\n      \"name\": \"${drop_rule.name}\",\n      \"id\": \"${drop_rule.resource.id}\",\n      \"pipeline_cloud_rule_entity_id\": ${length(local.validation_errors) == 0 ? "\"${drop_rule.resource.pipeline_cloud_rule_entity_id}\"" : "null"}\n    }"])}
                ]
            }
            EOT
            }

            # Success validation output - only shows meaningful content when all validations pass
            output "a_validation_success" {
            description = "Confirmation that all resources export pipeline_cloud_rule_entity_id"
            value       = length(local.validation_errors) == 0 ? "✅ All listed resources export pipeline_cloud_rule_entity_id" : null
            }

            # JSON output with all experimental drop rule IDs - only when validation passes
            output "experimental_drop_rule_resource_ids" {
            description = "JSON containing all drop rule resource names and IDs from experimental folder"
            value       = length(local.validation_errors) == 0 ? jsonencode(local.json_data_object) : null
            }

            # Formatted/Pretty JSON output for better readability - only when validation passes
            output "experimental_drop_rule_resource_ids_formatted" {
            description = "Pretty-formatted JSON containing all drop rule resource names and IDs from experimental folder"
            value       = length(local.validation_errors) == 0 ? local.formatted_json_string : null
            }

            # Validation output - only shows meaningful content when there are errors
            output "validation_errors" {
            description = "Lists any drop rule resources that do not export pipeline_cloud_rule_entity_id"
            value       = length(local.validation_errors) > 0 ? local.validation_errors : null
            }
            ```
          </Step>

          <Step>
            #### Update the script with your drop rule resources [#update-script]

            In the `locals` block of the script you just added, populate it with the drop rule resources you identified in step 1:

            ```hcl
            locals {
              experimental_drop_rule_resources = [
                {
                  name     = "drop_debug_logs"
                  resource = newrelic_nrql_drop_rule.drop_debug_logs
                },
                {
                  name     = "drop_health_checks"
                  resource = newrelic_nrql_drop_rule.drop_health_checks
                },
                {
                  name     = "drop_pii_data"
                  resource = newrelic_nrql_drop_rule.drop_pii_data
                }
              ]
            }
            ```
            <Callout title="Note">
              The `name` field should match the resource identifier and the `resource` field must match the full resource reference.
            </Callout>
          </Step>

          <Step>
            #### Run terraform plan [#run-terraform-plan]

            In your CI/CD environment, run:

            ```bash
            terraform plan
            ```

            If you see validation errors like this:

            ```
            Changes to Outputs:
              + validation_errors = [
                  + "❌ Drop rule 'drop_debug_logs' does not export `pipeline_cloud_rule_entity_id` or it is null",
                  + "❌ Drop rule 'drop_health_checks' does not export `pipeline_cloud_rule_entity_id` or it is null",
                  + "❌ Drop rule 'drop_pii_data' does not export `pipeline_cloud_rule_entity_id` or it is null",
                ]
            ```

            This means you're not on New Relic Terraform Provider >= v3.68.0. Upgrade your provider version in your Terraform configuration.

            If you see no errors, proceed to the next step.
          </Step>

          <Step>
            #### Run terraform apply [#run-terraform-apply]

            ```bash
            terraform apply
            ```

            After the apply completes, you'll see output like this:

            ```
            Outputs:

            a_validation_success = "✅ All listed resources export pipeline_cloud_rule_entity_id"
            experimental_drop_rule_resource_ids = "{\"drop_rule_resource_ids\":[{\"id\":\"3806526:106878882\",\"name\":\"drop_debug_logs\",\"pipeline_cloud_rule_entity_id\":\"MzgwNjUyNnxORVVMS...\"},{\"id\":\"3806526:106878883\",\"name\":\"drop_health_checks\",\"pipeline_cloud_rule_entity_id\":\"MzgwNjUyNnxORVVMS...\"},{\"id\":\"3806526:106878884\",\"name\":\"drop_pii_data\",\"pipeline_cloud_rule_entity_id\":\"MzgwNjUyNnxORVVMS...\"}]}"
            experimental_drop_rule_resource_ids_formatted = <<EOT
            {
              "drop_rule_resource_ids": [
                {
                  "name": "drop_debug_logs",
                  "id": "3806526:106878882",
                  "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
                },
                {
                  "name": "drop_health_checks",
                  "id": "3806526:106878883",
                  "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
                },
                {
                  "name": "drop_pii_data",
                  "id": "3806526:106878884",
                  "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
                }
              ]
            }
            ```
          </Step>

          <Step>
            #### Copy the JSON output [#copy-json-output]

            Copy the entire JSON string from the `experimental_drop_rule_resource_ids_formatted` output. You'll need this for Step 2.
          </Step>
        </Steps>
      </Collapser>

      <Collapser
        id="gitops-step2"
        title="2. Generate Pipeline cloud rule configuration locally"
      >
        In this step, you'll use the New Relic CLI to automatically generate Terraform configuration files for your Pipeline cloud rules based on the JSON output from Step 1.

        <Steps>
          <Step>
            #### Save the JSON output to a file [#save-json-output]

            Create a JSON file (for example, `drop_rules.json`) in your Terraform workspace directory and paste the JSON output from Step 1.

           ```json
           {
             "drop_rule_resource_ids": [
               {
                 "name": "drop_debug_logs",
                 "id": "3806526:106878882",
                 "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
               },
               {
                 "name": "drop_health_checks",
                 "id": "3806526:106878883",
                 "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
               },
               {
                 "name": "drop_pii_data",
                 "id": "3806526:106878884",
                 "pipeline_cloud_rule_entity_id": "MzgwNjUyNnxORVVMS..."
               }
             ]
           }
            ```
          </Step>

          <Step>
            #### Run the New Relic CLI migration command [#run-new-relic-cli-migration]

            Navigate to your Terraform workspace directory and run:

           ```bash
           newrelic migrate nrqldroprules tf-importgen-ci \
             --file drop_rules.json \
             --workspacePath /path/to/your/terraform/workspace
           ```

             <table>
             <thead>
               <tr>
                 <th style={{width: "200px"}}>Parameter</th>
                 <th>Type</th>
                 <th>Description</th>
                 <th>Required</th>
               </tr>
             </thead>
             <tbody>
               <tr>
                 <td>--file</td>
                 <td>String</td>
                 <td>Path to the JSON file containing your drop rule mappings from Step 1. Either <InlineCode>--file</InlineCode> or <InlineCode>--json</InlineCode> must be provided, but not both.</td>
                 <td>Yes*</td>
               </tr>
               <tr>
                 <td>--json</td>
                 <td>String</td>
                 <td>JSON string containing your drop rule mappings. Alternative to <InlineCode>--file</InlineCode>.</td>
                 <td>Yes*</td>
               </tr>
               <tr>
                 <td>--workspacePath</td>
                 <td>String</td>
                 <td>Path to your Terraform workspace. Defaults to current directory if omitted.</td>
                 <td>No</td>
               </tr>
               <tr>
                 <td>--tofu</td>
                 <td>Boolean</td>
                 <td>Use if you're using OpenTofu instead of Terraform.</td>
                 <td>No</td>
               </tr>
             </tbody>
           </table>

           **Example using `--json` parameter:**

           If you prefer not to create a file, you can pass the JSON string directly:

           ```bash
           newrelic migrate nrqldroprules tf-importgen-ci \
             --json '{"drop_rule_resource_ids":[{"name":"drop_debug_logs","id":"3806526:106878882","pipeline_cloud_rule_entity_id":"MzgwNjUyNnxORVVMS..."}]}' \
             --workspacePath /path/to/your/terraform/workspace
            ```
          </Step>

          <Step>
            #### Review the generated files [#review-generated-files]

            The CLI command generates four Terraform configuration files in your workspace:

            - **`provider.tf`:** Terraform and New Relic provider configuration.
            - **`imports.tf`:** Import blocks that link new resources to existing Pipeline cloud rules in New Relic.
            - **`pcrs.tf`:** Pipeline cloud rule resource definitions (generated during `terraform plan` with `-generate-config-out` flag).
            - **`removals.tf`:** Removed blocks to safely remove old `newrelic_nrql_drop_rule` resources from Terraform state without deleting them.

           {/* **Example of generated configuration:**

           ```hcl
           # provider.tf
           terraform {
             required_providers {
               newrelic = {
                 source  = "newrelic/newrelic"
                 version = "~> 3.68.0"
               }
             }
           }

           # imports.tf
           import {
             to = newrelic_pipeline_cloud_rule.drop_debug_logs
             id = "3806526:MzgwNjUyNnxORVVMS..."
           }

           import {
             to = newrelic_pipeline_cloud_rule.drop_health_checks
             id = "3806526:MzgwNjUyNnxORVVMS..."
           }

           import {
             to = newrelic_pipeline_cloud_rule.drop_pii_data
             id = "3806526:MzgwNjUyNnxORVVMS..."
           }

           # removals.tf
           removed {
             from = newrelic_nrql_drop_rule.drop_debug_logs
             lifecycle {
               destroy = false
             }
           }

           removed {
             from = newrelic_nrql_drop_rule.drop_health_checks
             lifecycle {
               destroy = false
             }
           }

           removed {
             from = newrelic_nrql_drop_rule.drop_pii_data
             lifecycle {
               destroy = false
             }
           }

           # pcrs.tf (generated during terraform plan)
           resource "newrelic_pipeline_cloud_rule" "drop_debug_logs" {
             account_id  = 3806526
             description = "Filters out debug level logs"
             action      = "drop"
             nrql        = "SELECT * FROM Log WHERE level = 'debug'"
           }

           resource "newrelic_pipeline_cloud_rule" "drop_health_checks" {
             account_id  = 3806526
             description = "Removes health check logs"
             action      = "drop"
             nrql        = "SELECT * FROM Log WHERE endpoint = '/health'"
           }

           resource "newrelic_pipeline_cloud_rule" "drop_pii_data" {
             account_id  = 3806526
             description = "Filters out PII data"
             action      = "drop"
             nrql        = "SELECT * FROM Log WHERE contains(message, 'SSN')"
           }
            ``` */}
          </Step>

          <Step>
            #### Commit and push the generated configuration [#commit-push-generated-configuration]

            Add the generated Terraform files to your git repository:

            ```bash
            git add .
            git commit -m "Add Pipeline cloud rule migration configuration"
            git push origin your-branch-name
            ```

            This will trigger your CI/CD pipeline to process the changes in Step 3.
          </Step>
        </Steps>
      </Collapser>

      <Collapser
        id="gitops-step3"
        title="3. Import Pipeline cloud rules in your CI/CD environment"
      >
        In this step, your CI/CD pipeline will process the changes you pushed in Step 2, import the Pipeline cloud rules into Terraform state, and remove the old drop rule resources.

        <Steps>
          <Step>
            #### Review the Terraform plan [#review-terraform-plan]

            After pushing your changes, your CI/CD pipeline will automatically trigger and post a plan comment on your pull request. The plan output will show:

           - **Resources being imported:** Three `newrelic_pipeline_cloud_rule` resources
           - **Resources being removed from state:** Three `newrelic_nrql_drop_rule` resources (with `destroy = false`)
           - **Plan summary:** `Plan: 3 to import, 0 to add, 0 to change, 0 to destroy`

           Sample Terraform plan output:

           ```diff
           Terraform will perform the following actions:

            # newrelic_nrql_drop_rule.drop_debug_logs will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_debug_logs" {
                  id                            = "3806526:106878882"
                  # (6 unchanged attributes hidden)
              }

            # newrelic_nrql_drop_rule.drop_health_checks will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_health_checks" {
                  id                            = "3806526:106878884"
                  # (6 unchanged attributes hidden)
              }

            # newrelic_nrql_drop_rule.drop_pii_data will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_pii_data" {
                  id                            = "3806526:106878885"
                  # (6 unchanged attributes hidden)
              }

           # newrelic_pipeline_cloud_rule.drop_debug_logs will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_debug_logs" {
                 account_id  = 3806526
                 description = "Filters out debug level logs from production environment to reduce data volume"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZjgtYmFmNy03MjU3LWE3M2MtZWY5OTkxYTQxMjgy"
                 name        = "NRQL drop rule ID: 106878882 created by user ID: 1004672904 created at: 2025-09-15T10:43:13.122888Z"
                 nrql        = "DELETE FROM `Log` WHERE ((`level` = 'debug') AND (`environment` = 'production'))"
             }

           # newrelic_pipeline_cloud_rule.drop_health_checks will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_health_checks" {
                 account_id  = 3806526
                 description = "Removes userEmail and userName attributes from MyCustomEvent data"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZmItMTQ0Yy03NDM5LWJhNDYtZjI4MTg0ODc5YmE2"
                 name        = "NRQL drop rule ID: 106878884 created by user ID: 1004672904 created at: 2025-09-15T10:45:47.049993Z"
                 nrql        = "DELETE `userEmail`, `userName` FROM `MyCustomEvent`"
             }

           # newrelic_pipeline_cloud_rule.drop_pii_data will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_pii_data" {
                 account_id  = 3806526
                 description = "Excludes containerId attribute from Metric aggregates"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZmItMTQ4Ni03MDI4LWJlMDktZmYzOTM2NWQ4ODUw"
                 name        = "NRQL drop rule ID: 106878885 created by user ID: 1004672904 created at: 2025-09-15T10:45:47.060296Z"
                 nrql        = "DELETE `containerId` FROM `MetricAggregate`"
             }

           Plan: 3 to import, 0 to add, 0 to change, 0 to destroy.
            ```

            <Callout variant="important">
              Note the message "will no longer be managed by Terraform, but will not be destroyed" confirms the old drop rules won't be deleted from New Relic.
            </Callout>
          </Step>

          <Step>
            #### Apply the changes [#apply-changes]

            Once you've reviewed the plan and confirmed it looks correct, comment on your pull request:

            ```
            Terraform apply
            ```

            Your CI/CD pipeline will execute the Terraform apply, which will:
            - Import the Pipeline cloud rules into Terraform state,
            - Remove the old drop rule resources from state (without deleting them from New Relic),
            - Update your Terraform state file,

            Sample apply output:

            ```
            newrelic_pipeline_cloud_rule.drop_health_checks: Importing...
            newrelic_pipeline_cloud_rule.drop_health_checks: Import complete
            newrelic_pipeline_cloud_rule.drop_debug_logs: Importing...
            newrelic_pipeline_cloud_rule.drop_debug_logs: Import complete
            newrelic_pipeline_cloud_rule.drop_pii_data: Importing...
            newrelic_pipeline_cloud_rule.drop_pii_data: Import complete

            Apply complete! Resources: 3 imported, 0 added, 0 changed, 0 destroyed.
            ```
          </Step>

          <Step>
            #### Verify the migration [#verify-migration]

            After the apply succeeds:

            - Check the output to confirm all imports completed successfully.
            - Verify in the New Relic UI that your Pipeline cloud rules are still active and functioning.
            - Run `terraform plan` in your CI/CD to confirm there are no pending changes (the output should show "No changes").
          </Step>

          <Step>
            #### Merge your pull request [#merge-pull-request]

            Once verification is complete, merge your pull request to finalize the migration. Your Terraform configuration now manages Pipeline cloud rules instead of NRQL drop rules.
          </Step>
        </Steps>
      </Collapser>
    </CollapserGroup>
  </Collapser>

  <Collapser
    id="local-terraform"
    title="Using local Terraform"
  >
    <CollapserGroup>
      <Collapser
        id="local-step1"
        title="1. Update Terraform state with Pipeline cloud rule mappings"
      >
        In this step, you'll use the New Relic CLI to automatically identify all drop rules in your Terraform workspace and update your Terraform state with their corresponding Pipeline cloud rule mappings.

        <Steps>
          <Step>
            #### Navigate to your Terraform workspace [#navigate-terraform-workspace]

            Open your terminal and navigate to the directory containing your Terraform configuration files with `newrelic_nrql_drop_rule` resources.

           ```bash
           cd /path/to/your/terraform/workspace
           ```
          </Step>

          <Step>
            #### Run the New Relic CLI update command [#run-new-relic-cli-update]

            Execute the following command to update your Terraform state:

           ```bash
           newrelic migrate nrqldroprules tf-update
           ```

            This command automatically:
            - Scans your Terraform workspace to identify all `newrelic_nrql_drop_rule` resources.
            - Runs `terraform plan` and `terraform apply` operations on these resources.
            - Updates your Terraform state file with the `pipeline_cloud_rule_entity_id` attribute for each drop rule.
          </Step>

          <Step>
            #### Verify the state update [#verify-state-update]

            After the command completes, your Terraform state file now contains the mapping between drop rules and Pipeline cloud rules. You can verify this by checking your state:

           ```bash
           terraform show -json | grep pipeline_cloud_rule_entity_id
           ```

            You should see the `pipeline_cloud_rule_entity_id` values for each drop rule in the output. This confirms your state file has been updated successfully. The CLI will read these values automatically in the next step.
          </Step>
        </Steps>
      </Collapser>

      <Collapser
        id="local-step2"
        title="2. Generate Terraform configuration files"
      >
        In this step, you'll use the New Relic CLI to automatically generate Terraform configuration files for your Pipeline cloud rules. The CLI reads the pipeline cloud rule mappings directly from your Terraform state file (updated in Step 1).

        <Steps>
          <Step>
            #### Run the New Relic CLI import generation command [#run-new-relic-cli-import-generation]

            In your Terraform workspace directory, run:

           ```bash
           newrelic migrate nrqldroprules tf-importgen-ci
           ```

            This command automatically:
            - Reads your Terraform state file to extract all `pipeline_cloud_rule_entity_id` values.
            - Generates import blocks for each Pipeline cloud rule.
            - Creates resource definitions for the Pipeline cloud rules.
            - Generates removal blocks for the old drop rule resources.

           <Callout variant="tip">
             Optional parameters:
             - `--workspacePath`: Specify a different Terraform workspace path (defaults to current directory)
             - `--tofu`: Use if you're using OpenTofu instead of Terraform
           </Callout>
          </Step>

          <Step>
            #### Review the generated files [#review-generated-files]

            The CLI command generates three main Terraform configuration files in your workspace:

            - `imports.tf`: Import blocks that link new resources to existing Pipeline cloud rules in New Relic.
            - `pcrs.tf`: Pipeline cloud rule resource definitions (will be populated during `terraform plan` in Step 3).
            - `removals.tf`: Removed blocks to safely remove old `newrelic_nrql_drop_rule` resources from Terraform state without deleting them from New Relic.

           The command also initializes the Terraform provider and prepares your workspace for the migration.
          </Step>
        </Steps>
      </Collapser>

      <Collapser
        id="local-step3"
        title="3. Apply the migration"
      >
        In this step, you'll execute the migration locally by running Terraform commands to import the Pipeline cloud rules into your state and remove the old drop rule resources. All files generated in Step 2 are already in your local workspace.

        <Steps>
          <Step>
            #### Run terraform plan with configuration generation [#run-terraform-plan]

            ```bash
            terraform plan -generate-config-out=pcrs.tf
            ```

            The `-generate-config-out` flag tells Terraform to automatically generate the configuration for the resources being imported and save them to `pcrs.tf`. The plan output will show:

           - **Resources being imported:** Three `newrelic_pipeline_cloud_rule` resources.
           - **Resources being removed from state:** Three `newrelic_nrql_drop_rule` resources (with `destroy = false`).
           - **Plan summary:** `Plan: 3 to import, 0 to add, 0 to change, 0 to destroy`.

           Sample Terraform plan output:

           ```diff
           Terraform will perform the following actions:

            # newrelic_nrql_drop_rule.drop_debug_logs will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_debug_logs" {
                  id                            = "3806526:106878882"
                  # (6 unchanged attributes hidden)
              }

            # newrelic_nrql_drop_rule.drop_health_checks will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_health_checks" {
                  id                            = "3806526:106878884"
                  # (6 unchanged attributes hidden)
              }

            # newrelic_nrql_drop_rule.drop_pii_data will no longer be managed by Terraform, but will not be destroyed
            # (destroy = false is set in the configuration)
            . resource "newrelic_nrql_drop_rule" "drop_pii_data" {
                  id                            = "3806526:106878885"
                  # (6 unchanged attributes hidden)
              }

           # newrelic_pipeline_cloud_rule.drop_debug_logs will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_debug_logs" {
                 account_id  = 3806526
                 description = "Filters out debug level logs from production environment to reduce data volume"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZjgtYmFmNy03MjU3LWE3M2MtZWY5OTkxYTQxMjgy"
                 name        = "NRQL drop rule ID: 106878882 created by user ID: 1004672904 created at: 2025-09-15T10:43:13.122888Z"
                 nrql        = "DELETE FROM `Log` WHERE ((`level` = 'debug') AND (`environment` = 'production'))"
             }

           # newrelic_pipeline_cloud_rule.drop_health_checks will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_health_checks" {
                 account_id  = 3806526
                 description = "Removes userEmail and userName attributes from MyCustomEvent data"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZmItMTQ0Yy03NDM5LWJhNDYtZjI4MTg0ODc5YmE2"
                 name        = "NRQL drop rule ID: 106878884 created by user ID: 1004672904 created at: 2025-09-15T10:45:47.049993Z"
                 nrql        = "DELETE `userEmail`, `userName` FROM `MyCustomEvent`"
             }

           # newrelic_pipeline_cloud_rule.drop_pii_data will be imported
             resource "newrelic_pipeline_cloud_rule" "drop_pii_data" {
                 account_id  = 3806526
                 description = "Excludes containerId attribute from Metric aggregates"
                 id          = "MzgwNjUyNnxOR0VQfFBJUEVMSU5FX0NMT1VEX1JVTEV8MDE5OTRjZmItMTQ4Ni03MDI4LWJlMDktZmYzOTM2NWQ4ODUw"
                 name        = "NRQL drop rule ID: 106878885 created by user ID: 1004672904 created at: 2025-09-15T10:45:47.060296Z"
                 nrql        = "DELETE `containerId` FROM `MetricAggregate`"
             }

           Plan: 3 to import, 0 to add, 0 to change, 0 to destroy.
            ```

            <Callout variant="important">
              Note the message "will no longer be managed by Terraform, but will not be destroyed" confirms the old drop rules won't be deleted from New Relic.
            </Callout>
          </Step>

          <Step>
            #### Apply the changes [#apply-changes]

            Once you've reviewed the plan and confirmed it looks correct, run:

           ```bash
           terraform apply
           ```

           Terraform will:
           - Import the Pipeline cloud rules into Terraform state.
           - Remove the old drop rule resources from state (without deleting them from New Relic).
           - Update your Terraform state file.

           Sample output:

           ```
           newrelic_pipeline_cloud_rule.drop_health_checks: Importing...
           newrelic_pipeline_cloud_rule.drop_health_checks: Import complete
           newrelic_pipeline_cloud_rule.drop_debug_logs: Importing...
           newrelic_pipeline_cloud_rule.drop_debug_logs: Import complete
           newrelic_pipeline_cloud_rule.drop_pii_data: Importing...
           newrelic_pipeline_cloud_rule.drop_pii_data: Import complete

           Apply complete! Resources: 3 imported, 0 added, 0 changed, 0 destroyed.
            ```
          </Step>

          <Step>
            #### Verify the migration [#verify-migration]

            After the apply succeeds:

            - Check the output to confirm all imports completed successfully.
            - Verify in the New Relic UI that your Pipeline cloud rules are still active and functioning.
            - Run `terraform plan` to confirm there are no pending changes (the output should show "No changes").
          </Step>

          <Step>
            #### Clean up the old drop rule resources [#clean-up-old-drop-rules]

            Once verification is complete, you can remove the old `newrelic_nrql_drop_rule` resource definitions from your original Terraform configuration files. Your Terraform configuration now manages Pipeline cloud rules instead of NRQL drop rules.
          </Step>
        </Steps>
      </Collapser>
    </CollapserGroup>
  </Collapser>
</CollapserGroup>

{/* The following content will go into a troubleshooting guide

## Troubleshooting [#troubleshooting]

### Import fails with "resource not found"

If imports fail during `terraform apply`:

**Solution:** Run `terraform refresh` to ensure your state is synchronized with New Relic, then verify the `pipeline_cloud_rule_entity_id` values are correct in your state file (for local approach) or JSON file (for GitOps approach).

### Plan shows unexpected changes after migration

If `terraform plan` shows changes after successful migration:

**Solution:** This usually indicates the generated `pcrs.tf` doesn't match the actual resource configuration. Review and adjust the generated configuration to match your New Relic settings exactly.

### Old drop rules still showing in Terraform state

If `terraform state list` still shows `newrelic_nrql_drop_rule` resources after migration:

**Solution:** Ensure your `removals.tf` file is properly configured with `destroy = false` and run `terraform apply` to remove them from state without deleting from New Relic. */}
