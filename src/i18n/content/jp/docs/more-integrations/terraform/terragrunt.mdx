---
title: Terragrunt を使用して複数の環境を管理する
metaDescription: 'Learn how to use [Terragrunt](https://www.terraform.io/) to manage configurations in multiple environments'
translationType: machine
---

[Terraform](https://www.terraform.io/)は、HashiCorp によって構築された、人気のあるコードとしてのインフラストラクチャ ソフトウェア ツールです。これを使用して、New Relic ダッシュボードやアラートなど、あらゆる種類のインフラストラクチャとサービスをプロビジョニングします。

[Terragrunt](https://terragrunt.gruntwork.io/#:~:text=Terragrunt%20is%20a%20thin%20wrapper,modules%2C%20and%20managing%20remote%20state.)は、Terraform の薄いラッパーであり、次の追加ツールを提供します。

* 繰り返しを減らす
* 複数の Terraform モジュールの操作
* リモート状態の管理

このガイドでは、Terragrunt を使用して次のことを行います。

* Terraform 構成を生成する
* ファイルの作成
* 複数の環境を管理する

<Video
  id="ivbp4e8t1f"
  type="wistia"
/>

## あなたが始める前に

このガイドを使用するには、New Relic と Terraform の両方に関する基本的な知識が必要です。

まだ行っていない場合:

* ホストに[New Relic Infrastructure エージェントをインストールする](https://docs.newrelic.com/docs/infrastructure/install-infrastructure-agent)
* [Terraform CLI をインストールする](https://www.terraform.io/intro/getting-started/install.html)
* [Terragrunt をインストールする](https://terragrunt.gruntwork.io/docs/getting-started/install/)

このガイドの例に従うために、 [GitHub](https://github.com/jsbnr/nr-terragrunt-intro-example)でサンプル コードを見つけることができます。

## コンフィギュレーションの作成

Terragrunt は、Terraform 構成用の追加ツールを提供します。ここでは、Terragrunt、Terraform、および New Relic を使用して構成を作成します。

<Steps>
  <Step>
    ワークスペースを初期化します。

    ```bash
    mkdir terragrunt-config && cd terragrunt-config
    ```
  </Step>

  <Step>
    新しいフォルダーで、 _terragrunt.hcl_ファイルを作成します。

    ```bash
    touch terragrunt.hcl
    ```
  </Step>

  <Step>
    次に、 _dev_というサブフォルダーを含む_環境_フォルダーを作成します。

    ```bash
    mkdir -p environments/dev
    ```
  </Step>

  <Step>
    次に、 _main.tf_ファイルと_provider.tf_ファイルを含む_src_フォルダーを作成します。

    ```bash
    mkdir src
    touch src/main.tf
    touch src/provider.tf
    ```

    _main.tf_で Terraform リソースを構成し、 provider.tf で_プロバイダー_を構成します。
  </Step>

  <Step>
    _src/provider.tf_で、[New Relic プロバイダー](https://registry.terraform.io/providers/newrelic/newrelic/latest/docs)を構成します。

    ```hcl
    terraform {
    	required_version = "~> 0.14.3"
    	required_providers {
    		newrelic = {
    			source = "newrelic/newrelic"
    			version = "~> 2.14.0"
    		}
    	}
    }
    ```
  </Step>

  <Step>
    _src/main.tf_で、`DemoPolicy`という名前の New Relic アラート ポリシーを追加します。

    ```hcl
    resource "newrelic_alert_policy" "DemoPolicy" {
    	name = "My Demo Policy"
    }
    ```
  </Step>

  <Step>
    _environment/dev_で、 _terragrunt.hcl_という名前のファイルを作成します。

    ```bash
    touch environments/dev/terragrunt.hcl
    ```
  </Step>

  <Step>
    その中に、次の`include`ステートメントを追加します。

    ```hcl
    include {
    	path = find_in_parent_folders()
    }
    ```

    これは、Terragrunt に任意の .hcl を使用するように指示します。親フォルダーで見つかった構成ファイル。
  </Step>

  <Step>
    [`terraform`](https://terragrunt.gruntwork.io/docs/reference/config-blocks-and-attributes/#terraform)ブロックを追加して、Terragrunt にソース参照を提供します。

    ```hcl
    include {
    	path = find_in_parent_folders()
    }

    terraform {
    	source = "../../src"
    }
    ```
  </Step>

  <Step>
    _src/provider.tf_で、API キー、アカウント ID、リージョンを使用して New Relic プロバイダーを構成します。

    ```hcl
    terraform {
        required_version = "~> 0.14.3"
        required_providers {
            newrelic = {
                source = "newrelic/newrelic"
                version = "~> 2.14.0"
            }
        }
    }

    variable "newrelic_personal_apikey" {}
    variable "newrelic_account_id" {}
    variable "newrelic_region" {}

    provider "newrelic" {
    	account_id = var.newrelic_account_id
    	api_key = var.newrelic_personal_apikey
    	region = var.newrelic_region
    }
    ```

    変数を使用して、構成を動的に保ちます。
  </Step>

  <Step>
    _environment/dev_で、terragrunt を初期化します。

    ```bash
    terragrunt init
    ```

    これにより、環境変数を含む少しのコンテキストが設定され、 `terraform init`が実行されます。

    ```bash copyable=false
    [output] Terraform has created a lock file .terraform.lock.hcl to record the provider
    [output] selections it made above. Include this file in your version control repository
    [output] so that Terraform can guarantee to make the same selections by default when
    [output] you run "terraform init" in the future.
    [output]
    [output] Terraform has been successfully initialized!
    [output]
    [output] You may now begin working with Terraform. Try running "terraform plan" to see
    [output] any changes that are required for your infrastructure. All Terraform commands
    [output] should now work.
    [output]
    [output] If you ever set or change modules or backend configuration for Terraform,
    [output] rerun this command to reinitialize your working directory. If you forget, other
    [output] commands will detect it and remind you to do so if necessary.
    [output] [terragrunt] [/workspace/terragrunt-config/environments/dev] 2021/02/02 13:30:31 Copying lock file [output] from /workspace/terragrunt-config/environments/dev/.terragrunt-cache/e-PoBgWhdv3v8QGOtDQxS_WeYu4/
    [output] 69zjIFUfApJiUt8gFmi-6-dcPe8/.terraform.lock.hcl to /workspace/terragrunt-config/environments/dev
    ```
  </Step>

  <Step>
    _environment/dev/terragrunt.hcl_で、 `inputs`ブロックを追加して、New Relic アカウント変数の値を提供します。

    ```hcl
    inputs = {
    	newrelic_personal_apikey = "NRAK-***" # Your New Relic account ID
    	newrelic_account_id = "12345" # Your New Relic account ID
    	newrelic_region = "US" # US or EU (defaults to US)
    }
    ```
  </Step>

  <Step>
    次に、 `terragrunt plan`を実行します。

    ```bash copyable=false
    [output] An execution plan has been generated and is shown below.
    [output] Resource actions are indicated with the following symbols:
    [output]  + create
    [output]
    [output] Terraform will perform the following actions:
    [output]
    [output]  # newrelic_alert_policy.DemoPolicy will be created
    [output]  + resource "newrelic_alert_policy" "DemoPolicy" {
    [output]    + account_id     = (known after apply)
    [output]    + id         = (known after apply)
    [output]    + incident_preference = "PER_POLICY"
    [output]    + name        = "My Demo Policy"
    [output]   }
    [output]
    [output] Plan: 1 to add, 0 to change, 0 to destroy.
    [output]
    [output] ------------------------------------------------------------------------
    [output]
    [output] Note: You didn't specify an "-out" parameter to save this plan, so Terraform
    [output] can't guarantee that exactly these actions will be performed if
    [output] "terraform apply" is subsequently run.
    ```

    Terragrunt は`inputs`ブロックの値を提供します。
  </Step>

  <Step>
    `terragrunt apply`を実行:

    ```bash
    terragrunt apply
    ```

    これで、デモ ポリシーが New Relic アカウントに追加されました。
  </Step>
</Steps>

## 構成に追加

基本的な New Relic 構成を作成したので[、Terraform](/terraform/get-started-terraform)および[Terraform モジュール](/terraform/terraform-modules)ガイドから構成を追加します。

<Callout variant="tip">
  これらのガイドをまだ行っていない場合は、 [Terragrunt intro Github リポジトリ](https://github.com/jsbnr/nr-terragrunt-intro-example)から構成をコピーできます。
</Callout>

<Steps>
  <Step>
    _src/main.tf_で、アラート チャネルの電子メール アドレスを希望の電子メール アドレスに更新します。

    ```hcl
    resource "newrelic_alert_policy" "DemoPolicy" {
    	name = "My Demo Policy"
    }

    resource "newrelic_alert_channel" "DemoChannel" {
    	name = "My Demo Channel"
    	type = "email"

    	config {
    		recipients = "your@email_address.com"
    		include_json_attachment = "1"
    	}
    }

    resource "newrelic_alert_policy_channel" "ChannelSubs" {
    	policy_id = newrelic_alert_policy.DemoPolicy.id
    	channel_ids = [
    		newrelic_alert_channel.DemoChannel.id
    	]
    }

    module "HostConditions" {
    	source = "git::https://github.com/jsbnr/demo-terraform.git"
    	policyId = newrelic_alert_policy.DemoPolicy.id
    	cpu_critical = 88
    	cpu_warning = 78
    	diskPercent = 68
    }
    ```

    ここでは、New Relic アラート チャネルを追加し、デモ ポリシーをアラート チャネルにサブスクライブし、Github でホストされているモジュールを追加しました。
  </Step>

  <Step>
    `terragrunt init`を実行してから`terragrunt apply`を実行します。

    ```bash
    terragrunt init
    terragrunt apply
    ```

    Terraform が処理を終了すると、アラート ポリシーには 2 つの条件と 1 つのアラート チャネルが含まれます。
  </Step>
</Steps>

## 環境を Terragrunt 変数として使用する

Terragrunt を使用すると、実行中の環境の名前を作成中のデータの名前に追加して、New Relic でリソースをより識別しやすくすることができます。

<Steps>
  <Step>
    ルート_terragrunt.hcl_ファイルで、 `env_name`の入力を作成します。

    ```hcl
    inputs = {
    	env_name = "develop"
    }
    ```
  </Step>

  <Step>
    _src/main.tf_で、ファイルに`env_name`という名前の新しい変数を追加します:

    ```hcl
    variable "env_name" {}
    ```
  </Step>

  <Step>
    新しい`env_name`変数をアラート ポリシーとアラート チャネル リソース ブロックに追加します。

    ```hcl
    resource "newrelic_alert_policy" "DemoPolicy" {
    	name = "${var.env_name}: My Demo Policy"
    }

    resource "newrelic_alert_channel" "DemoChannel" {
    	name = "${env_name}: My Demo Channel"
    	type = "email"

    	config {
    		recipients = "your@email_address.com"
    		include_json_attachment = "1"
    	}
    }
    ```
  </Step>

  <Step>
    `terragrunt plan`を実行して、ポリシー名に追加された環境変数を確認します。

    ```bash copyable=false
    [output] # newrelic_alert_policy.DemoPolicy will be updated in-place
    [output] ~ resource "newrelic_alert_policy" "DemoPolicy" {
    [output]    id         = "1216533"
    [output]   ~ name        = "My Demo Policy" -> "develop: My Demo Policy"
    [output]    # (2 unchanged attributes hidden)
    [output]  }
    [output]
    [output] # newrelic_alert_policy_channel.ChannelSubs must be replaced
    [output] -/+ resource "newrelic_alert_policy_channel" "ChannelSubs" {
    [output]   ~ channel_ids = [
    [output]     - 4737437,
    [output]    ] -> (known after apply) # forces replacement
    [output]   ~ id     = "1216533:4737437" -> (known after apply)
    [output]    # (1 unchanged attribute hidden)
    [output]  }
    ```

    ここでは、環境を_terragrunt.hcl_にハードコードしました。これをより動的にするには、terragrunt 組み込み関数を使用して環境を取得します。
  </Step>

  <Step>
    ルート_terragrunt.hcl_ファイルで、入力を更新して`path_relative_to_include()`を使用し、値を`env_name`変数として渡します。

    ```hcl
    inputs = {
    	env_name = path_relative_to_include()
    }
    ```
  </Step>

  <Step>
    `terragrunt plan`を実行:

    ```bash copyable=false
    [output] # newrelic_alert_policy.DemoPolicy will be updated in-place
    [output] ~ resource "newrelic_alert_policy" "DemoPolicy" {
    [output]    id         = "1216533"
    [output]   ~ name        = "My Demo Policy" -> "environments/dev: My Demo Policy"
    [output]    # (2 unchanged attributes hidden)
    [output]  }
    [output]
    [output] # newrelic_alert_policy_channel.ChannelSubs must be replaced
    [output] -/+ resource "newrelic_alert_policy_channel" "ChannelSubs" {
    [output]   ~ channel_ids = [
    [output]     - 4737437,
    [output]    ] -> (known after apply) # forces replacement
    [output]   ~ id     = "1216533:4737437" -> (known after apply)
    [output]    # (1 unchanged attribute hidden)
    [output]  }
    [output]
    [output] Plan: 2 to add, 1 to change, 2 to destroy.
    ```

    `env_name`変数には`./environments/dev/`パス全体が含まれていることに注意してください。代わりに、「dev」部分のみを含めます。
  </Step>

  <Step>
    _terragrunt.hcl_を更新して、 `env_name`から「environements/」を削除します。

    ```hcl
    locals {
    	env_name = replace(path_relative_to_include(), "environments/", "")
    }

    inputs = {
    	env_name = local.env_name
    }
    ```

    ここでは、 `locals`ブロックを追加してローカル変数を作成し、組み込みの`replace`関数を使用して相対パスの不要な部分を削除しました。次に、ローカル変数を使用するように`inputs`ブロックを更新しました。
  </Step>

  <Step>
    `terragrunt plan`を実行:

    ```bash copyable=false
    [output]  # newrelic_alert_policy.DemoPolicy will be updated in-place
    [output]  ~ resource "newrelic_alert_policy" "DemoPolicy" {
    [output]     id         = "1216533"
    [output]    ~ name        = "My Demo Policy" -> "dev: My Demo Policy"
    [output]     # (2 unchanged attributes hidden)
    [output]   }
    [output]
    [output]  # newrelic_alert_policy_channel.ChannelSubs must be replaced
    [output] -/+ resource "newrelic_alert_policy_channel" "ChannelSubs" {
    [output]    ~ channel_ids = [
    [output]      - 4737437,
    [output]     ] -> (known after apply) # forces replacement
    [output]    ~ id     = "1216533:4737437" -> (known after apply)
    [output]     # (1 unchanged attribute hidden)
    [output]   }
    [output]
    [output] Plan: 2 to add, 1 to change, 2 to destroy.
    ```

    新しいポリシー名は「dev: My Demo Policy」です。
  </Step>

  <Step>
    `terragrunt apply`を実行して構成を更新します。

    ```bash
    terragrunt apply
    ```
  </Step>
</Steps>

## 状態をリモート ストレージに移動する

現時点では、状態ファイルはローカルです。ここで、Terraform 構成を更新して Amazon S3 に保存します。

<Callout variant="tip">
  Terragrunt では複数の環境を構成できるため、状態ファイルを独自の S3 バケットに保存して、互いに上書きしないようにする必要があります。
</Callout>

<Steps>
  <Step>
    開発状態ファイル用[の S3 バケットを作成し](https://docs.aws.amazon.com/AmazonS3/latest/userguide/creating-bucket.html)ます。
  </Step>

  <Step>
    [バケットにファイルを保存する権限を](https://docs.aws.amazon.com/IAM/latest/UserGuide/reference_policies_examples_s3_rw-bucket.html)持つ[IAM ユーザーを作成](https://docs.aws.amazon.com/IAM/latest/UserGuide/intro-structure.html)します。
  </Step>

  <Step>
    ルート_terragrunt.hcl_で、ファイルを S3 のどこに配置するかを Terragrunt に指示する`remote_state`ブロックを追加します。

    ```hcl
    remote_state {
    	backend = "s3"
    	generate = {
    		path = "backend.tf"
    		if_exists = "overwrite_terragrunt"
    	}
    	config = {
    		bucket = "YOUR_S3_BUCKET_NAME" # Amazon S3 bucket required

    		key     = "envs/${local.env_name}/terraform.tfstate"
    		region  = "us-east-1"
    		encrypt = true
    		profile = "YOUR_PROFILE_NAME" # Profile name required
    	}
    }
    ```

    ここでは、バケット名、リージョン、暗号化、およびプロファイルを指定するリモート状態構成を定義しました。プレースホルダーの値を実際の値に置き換えてください。`key`では、前に作成したローカルの`env_name`を使用して、状態ファイルの環境を動的に設定しました。最後に、バケットに_backend.tf_という名前の新しいファイルを生成するよう Terragrunt に指示しました。
  </Step>

  <Step>
    `terragrunt plan`を実行:

    ```bash
    terragrunt plan
    ```

    バケットに、 _envs という名前のフォルダーが表示されます。_その中には、 _terraform.tfstate_ファイルを含む_devs_というフォルダーがあります。

    <Callout variant="tip">
      `envs/dev`内には、 _terragrunt-cache_という名前の隠しフォルダーがあります。その中には、Terragrunt が生成した_backend.tf_ファイルがあります。
    </Callout>
  </Step>
</Steps>

## 新しい環境を作成する

開発環境を構成したので、ほとんどの作業を再利用する別の環境を作成します。

<Steps>
  <Step>
    _環境_の下に、 _nonprod_という名前のフォルダーを作成します。その中に、 _terragrunt.hcl_というファイルを作成します。

    ```bash
    mkdir nonprod && cd nonprod
    touch terragrunt.hcl
    ```
  </Step>

  <Step>
    _environment/nonprod/terragrunt.hcl_で、 _environments/dev/terragrunt.hcl_から構成をコピーします。

    ```hcl
    include {
    	path= find_in_parent_folders()
    }

    terraform {
    	source = "../../src"
    }

    inputs = {
    	newrelic_personal_apikey = "NRAK-***" # Your New Relic account ID
    	newrelic_account_id = "12345" # Your New Relic account ID
    	newrelic_region = "US" # US or EU (defaults to US)
    }
    ```

    <Callout variant="tip">
      非本番環境用に別のアカウントを使用している場合は、新しいアカウント ID、API キー、リージョンで`inputs`を更新してください。
    </Callout>
  </Step>

  <Step>
    _nonprod_内で、 `terragrunt init`と`terragrunt apply`を実行します。

    ```bash
    terragrunt init
    terragrunt apply
    ```

    Terraform は、「nonprod:」で始まる新しいリソース セットを作成します。

    これで、dev と nonprod という 2 つの環境を作成しましたが、名前以外は同じです。
  </Step>

  <Step>
    _src/main.tf_で、Host Conditions モジュールに新しい変数を追加します。

    ```hcl
    variable "cpu_critical" {default = 89}
    variable "cpu_warningl" {default = 79}
    variable "diskPercentage" {default = 69}
    ```

    このような変数を使用すると、構成がより動的になります。
  </Step>

  <Step>
    `cpu_critical` 、 `cpu_warning` 、および`diskPercentage`変数を使用するように`HostConditions`を更新します。

    ```hcl
    module "HostConditions" {
    	source = "git::https://github.com/jsbnr/demo-terraform.git"
    	policyId = newrelic_alert_policy.DemoPolicy.id
    	cpu_critical = var.cpu_critical
    	cpu_warning = var.cpu_warninig
    	diskPercent = var.dskPercentage
    }
    ```
  </Step>

  <Step>
    `terragrunt plan`を実行:

    ```bash
    terragrunt plan
    ```

    `HostConditions`値に変数のデフォルトが含まれるようになりました。
  </Step>

  <Step>
    _nonprod/terragrunt.hcl_で、変数に値を追加します。

    ```hcl
    inputs = {
    	newrelic_personal_apikey = "NRAK-***" # Your New Relic account ID
    	newrelic_account_id = "12345" # Your New Relic account ID
    	newrelic_region = "US" # US or EU (defaults to US)

    	cpu_critical = 50
    	cpu_warninig = 40
    	diskPercentage = 98
    }
    ```

    これにより、値が環境構成に渡されます。
  </Step>

  <Step>
    `terragrunt apply`を実行:

    ```bash
    terragrunt apply
    ```

    New Relic アカウントには、製品固有ではない構成を持つ新しいポリシーがあります。
  </Step>
</Steps>

## 結論

おめでとう！ Terragrnt を使用して、New Relic 構成を生成し、複数の環境を管理しました。[Terragrunt の紹介例](https://github.com/jsbnr/nr-terragrunt-intro-example)、 [New Relic Terraform プロバイダーのドキュメント](https://registry.terraform.io/providers/newrelic/newrelic/latest/docs)、 [Terragrunt のクイック スタート](https://terragrunt.gruntwork.io/docs/getting-started/quick-start/)を確認して、構成を次のレベルに引き上げる方法を学習してください。